global string $inputField;
global string $operationMenu;
global string $resultField;
global proc float add(float $input[]) {
    float $result = 0;
    for ($i = 0; $i < size($input); $i++) {
        $result += $input[$i];
    }
    return $result;
}

global proc float subtract(float $input[]) {
    float $result = $input[0];
    for ($i = 1; $i < size($input); $i++) {
        $result -= $input[$i];
    }
    return $result;
}

global proc float multiply(float $input[]) {
    float $result = 1;
    for ($i = 0; $i < size($input); $i++) {
        $result *= $input[$i];
    }
    return $result;
}

global proc float divide(float $input[]) {
    float $result = $input[0];
    for ($i = 1; $i < size($input); $i++) {
        $result /= $input[$i];
    }
    return $result;
}

global proc float power(float $input[]) {
    float $result = $input[0];
    for ($i = 1; $i < size($input); $i++) {
        $result = pow($result, $input[$i]);
    }
    return $result;
}

global proc float mean(float $input[]) {
    float $result = 0;
    for ($i = 0; $i < size($input); $i++) {
        $result += $input[$i];
    }
    return $result / size($input);
}

global proc float median(float $input[]) {
    float $result;
    int $size = size($input);
    float $sorted[];
    for ($i = 0; $i < $size; $i++) {
        $sorted[$i] = $input[$i];
    }
    for ($i = 0; $i < $size; $i++) {
        for ($j = 0; $j < $size - $i - 1; $j++) {
            if ($sorted[$j] > $sorted[$j + 1]) {
                float $temp = $sorted[$j];
                $sorted[$j] = $sorted[$j + 1];
                $sorted[$j + 1] = $temp;
            }
        }
    }
    if ($size % 2 == 0) {
        $result = ($sorted[$size / 2] + $sorted[$size / 2 - 1]) / 2;
    } else {
        $result = $sorted[$size / 2];
    }
    return $result;
}

global proc float Calculator(float $input[], string $operation) {
    float $result;
    string $operationString;
    switch ($operation) {
        case "add":
            $result = add($input);
            $operationString = " + ";
            break;
        case "subtract":
            $result = subtract($input);
            $operationString = " - ";
            break;
        case "multiply":
            $result = multiply($input);
            $operationString = " * ";
            break;
        case "divide":
            $result = divide($input);
            $operationString = " / ";
            break;
        case "power":
            $result = power($input);
            $operationString = " ^ ";
            break;
        case "mean":
            $result = mean($input);
            $operationString = " mean ";
            break;
        case "median":
            $result = median($input);
            $operationString = " median ";
            break;
        default:
            error("That's not an operation");
            return 0;
    }

    string $inputString = "";
    for ($i = 0; $i < size($input); $i++) {
        $inputString += $input[$i];
        if ($i < size($input) - 1) {
            $inputString += ", ";
        }
    }

    if ($operation == "mean" || $operation == "median") {
        print("The " + $operation + " of " + $inputString + " is " + $result + "\n");
    } else {
        print($inputString + $operationString + "= " + $result + "\n");
    }

    return $result;
}

global proc createCalculatorUI() {
    // Declare global variables to be used inside the procedure
    global string $inputField;
    global string $operationMenu;
    global string $resultField;
    
    if (`window -exists calculatorWindow`) {
        deleteUI calculatorWindow;
    }
    
    window -title "Braedawg's Calculator" -widthHeight 300 200 calculatorWindow;
    columnLayout -adjustableColumn true;
    
    text -label "Enter numbers (separated by a comma):";
    // Store the control name in a variable
    $inputField = `textField -w 200 -text ""`;
    
    text -label "Select operation:";
    // Store the control name in a variable
    $operationMenu = `optionMenu`;
        menuItem -label "add";
        menuItem -label "subtract";
        menuItem -label "multiply";
        menuItem -label "divide";
        menuItem -label "power";
        menuItem -label "mean";
        menuItem -label "median";
    
    button -label "Calculate" -command "performCalculation()";
    
    text -label "Result:";
    // Store the control name in a variable
    $resultField = `textField -editable false`;
    
    showWindow calculatorWindow;
}


global proc performCalculation() {
    // Declare global variables to access the control names
    global string $inputField;
    global string $operationMenu;
    global string $resultField;
    
    // Query the text field and option menu using the stored control names
    string $inputString = `textField -q -text $inputField`;
    string $operation = `optionMenu -q -value $operationMenu`;
    
    // Process the input string
    string $inputArray[] = stringToStringArray($inputString, ",");
    float $input[];
    for ($i = 0; $i < size($inputArray); $i++) {
        $input[$i] = float($inputArray[$i]);
    }
    
    // Calculate the result
    float $result = Calculator($input, $operation);
    
    // Update the result field
    textField -edit -text (string($result)) $resultField;
}

createCalculatorUI();